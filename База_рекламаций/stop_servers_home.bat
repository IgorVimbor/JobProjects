@echo off
:: Устанавливаем кодировку UTF-8 для корректного отображения русских символов
chcp 65001 > nul
title Stop servers
echo        ========================================
echo                  Остановка серверов
echo        ========================================

:: 1. Останавливаем сервер Nginx
echo.
echo        Остановка сервера Nginx...
timeout /t 1 /nobreak > nul

cd /d "C:\nginx-1.28.0"
:: Добавлено 2>nul, чтобы скрыть ошибку "файл не найден" если используем dev-сервер.
.\nginx.exe -s stop 2>nul
::.\nginx.exe -s stop

:: Выводим информационное сообщение
echo        Nginx остановлен!
timeout /t 1 /nobreak > nul
:: ---------------------------------------------------------------------------------------------

:: 2. Принудительно останавливаем сервер Django (runserver или Waitress)
echo.
echo        Остановка сервера Django...
timeout /t 1 /nobreak > nul
:: Находим окно с заголовком "Django" и принудительно завершаем его (/F)
:: Перенаправляем вывод > nul, чтобы скрыть сообщение об успешном завершении
taskkill /FI "WINDOWTITLE eq Django" /T /F > nul
echo        Django остановлен!
:: ---------------------------------------------------------------------------------------------

:: 3. ЗАПУСК СКРИПТА БЭКАПА И КОММИТА
echo.
echo        ==========================================
echo                  Запуск бэкапа и коммита
echo        ==========================================
echo.

:: Переходим в корень проекта и активируем виртуальное окружение
cd /d "D:\MyRepositories\JobProjects\База_рекламаций"
call rhub_venv\Scripts\activate

:: Переходим в папку проекта
cd reclamationhub

:: Запускаем Python-скрипт
echo        Выполняется Python-скрипт backup_and_commit.py...
echo.
echo (При возникновении ошибок смотри подробности в логах или в окне)
python backup_and_commit.py

echo.
echo        Бэкап и коммит завершены.
:: ---------------------------------------------------------------------------------------------

:: 4. Запуск Python-скрипта email_send.py отправки письма с фикстурой БД
echo.
echo        Выполняется Python-скрипт email_send.py...
echo.
python email_send.py
:: ---------------------------------------------------------------------------------------------

echo.
timeout /t 2 /nobreak > nul
echo        ================================================
echo        Серверы остановлены и операции бэкапа завершены!
echo        ================================================
echo.
echo        Это окно закроется автоматически...
timeout /t 3 /nobreak > nul
:: Закрываем окно
:: exit
echo.
echo        ....... Нажмите любую клавишу для закрытия этого окна .......
echo.
pause
:: ---------------------------------------------------------------------------------------------